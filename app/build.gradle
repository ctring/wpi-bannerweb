import groovy.swing.SwingBuilder

apply plugin: 'com.android.application'

android {
    signingConfigs {
        config {
            keyAlias 'totoro'
            keyPassword ''
            storeFile file('../../totoro.jks')
            storePassword ''
        }

    }
    compileSdkVersion 22
    buildToolsVersion "22.0.1"
    defaultConfig {
        applicationId "com.cuongnd.wpibannerweb"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        signingConfig signingConfigs.config
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    if(taskGraph.hasTask(':app:assembleRelease')) {
        def storePass = ''
        def keyPass = ''
        if(System.console() == null) {
            new SwingBuilder().edt {
                dialog(modal: true,
                        title: 'Enter password',
                        alwaysOnTop: true,
                        resizable: false,
                        locationRelativeTo: null,
                        pack: true,
                        show: true)
                        {
                            vbox {
                                label(text: "Please enter store passphrase:")
                                def input1 = passwordField()
                                label(text: "Please enter key passphrase:")
                                def input2 = passwordField()
                                button(defaultButton: true, text: 'OK', actionPerformed: {
                                    storePass = input1.password;
                                    keyPass = input2.password;
                                    dispose();
                                })
                            }
                        }
            }
        } else {
            storePass = System.console().readPassword("\nPlease enter store passphrase: ")
            keyPass = System.console().readPassword("\nPlease enter key passphrase: ")
        }

        if(storePass.size() <= 0 || keyPass.size() <= 0) {
            throw new InvalidUserDataException("You must enter the passwords to proceed.")
        }

        storePass = new String(storePass)
        keyPass = new String(keyPass)

        android.signingConfigs.config.storePassword = storePass
        android.signingConfigs.config.keyPassword = keyPass
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.github.alamkanak:android-week-view:1.2.3'
    compile 'org.jsoup:jsoup:1.7.2'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:cardview-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
}